Require Import guarding.lib.boxes.
Require Import lrust.lifetime.lifetime_internals.ra.
Require Import lrust.lifetime.lifetime_internals.inv.

Require Import stdpp.base.
From iris.algebra Require Export cmra updates.
From iris.algebra Require Import proofmode_classes.
From iris.algebra Require Import excl.
From iris.prelude Require Import options.

From iris.base_logic.lib Require Export own iprop.
From iris.proofmode Require Import base proofmode.
From iris.proofmode Require Import proofmode.
From iris.proofmode Require Import proofmode.
From iris.proofmode Require Import proofmode.
From iris.base_logic.lib Require Export invariants.

Section FullBorrows.
  Context {Σ: gFunctors}.
  Context `{!llft_logicGS Σ}.
  Context `{!invGS_gen hlc Σ}.
  Context `{!boxG Σ}.
  
  Context (γ: gname).
  Context (γo: gname).
  Context (γd: gname).
  
  Implicit Types (mprops: gmap_props Σ).
  
  Local Instance modulo_dead_pers γ1 k : Persistent (ModuloDeadPers γ1 k).
  Proof. apply own_core_persistent. unfold CoreId. trivial. Qed.
 
  Lemma future_vs_create_empty alive dead outlives mbs mprops sn P dd :
  ¬(dd ## dead) →
  (mbs !! sn = None) →
  future_vs γ alive dead outlives mbs mprops
  ⊢ future_vs γ alive dead outlives (<[sn:=Borrow ∅ dd]> mbs) (<[sn:=P]> mprops).
  Proof using invGS_gen0 Σ γ γd γo.
    generalize dead. clear dead.
    induction alive as [my_alive IH] using set_ind_strong. 
    iIntros (my_dead Hdd Hmbssn) "Hvs".
    
    rewrite future_vs_def.
    rewrite (future_vs_def γ my_alive).
    
    iIntros (k) "%Hin_my_alive #Dead". iSplit.
      { iIntros "NotSwell". iDestruct ("Hvs" $! k Hin_my_alive with "Dead") as "[Hvs _]".
        iApply IH. { intuition. } { set_solver. } { trivial. }
        iApply "Hvs". iFrame.
      }
    iIntros "inval".
    iDestruct ("Hvs" $! k Hin_my_alive with "Dead") as "[_ Hvs]".
    
    iDestruct (full_borrows_invalidated_via_insert_false my_alive my_dead k sn mbs mprops (Borrow ∅ {[default_dead]}) P with "inval") as "inval"; trivial.
    
    iMod ("Hvs" with "[inval]") as "[reval vs]". { iFrame. }
    
    iModIntro.
    
    iSplitL "reval". {
      iApply full_borrows_revalidated_via_insert_false; trivial.
      unfold revalidated. rewrite bool_decide_eq_false. set_solver.
    }
    
    iApply IH; trivial. { set_solver. } { set_solver. }
  Qed.
  
  Lemma inner_borrow_create_empty alive dead blocked P :
      (▷ inner_inv γ γo alive dead blocked) ∗ (▷ P)
        ={↑Nbox}=∗
        ∃ sn , (▷ inner_inv γ γo alive dead blocked)
            ∗ OwnBorState γ sn (Borrow ∅ {[ default_dead ]})
            ∗ slice Nbox sn P.
  Proof using invGS_gen0 Σ γ γd γo.
    iIntros "[Inv P]".
    unfold inner_inv.
    iDestruct "Inv" as (mbs mprops Ptotal outlives) "[>auth [>Modulo [box [vs [>%pures [#slices outlives]]]]]]".
    
    iMod (slice_insert_full Nbox (↑Nbox) true (boxmap alive dead mbs) Ptotal P with "P box") as (sn) "[%Hisnone [#slice box]]"; trivial.
    assert (mbs !! sn = None) as Hmbssn. {
      destruct (mbs !! sn) eqn:Heqn; trivial.
      rewrite lookup_fmap in Hisnone. rewrite Heqn in Hisnone. discriminate.
    }
    iMod (alloc_bor_state γ sn mbs (Borrow ∅ {[ default_dead ]}) with "auth") as "[auth OwnBor]"; trivial.
    
    assert (default_dead ∈ dead) as Hdd. { unfold inv.map_wf in pures. intuition. }
    assert (¬({[default_dead]} ## dead)) as Hdd2. { set_solver. }
    
    iDestruct (bi.later_mono _ _ (future_vs_create_empty alive dead outlives mbs mprops sn P {[default_dead]} Hdd2 Hmbssn) with "vs") as "vs".
    
    iModIntro. iExists sn. iFrame "OwnBor". iFrame "slice". iFrame "Modulo".
    iNext.
    iExists (<[sn:=Borrow ∅ {[default_dead]}]> mbs).
    iExists (<[sn:=P]> mprops).
    iExists (P ∗ Ptotal)%I.
    iExists outlives.
    iFrame "auth".
    iSplitL "box". { 
      rewrite boxmap_insert_Borrow.
      rewrite bool_decide_eq_true_2. { iFrame. }
      set_solver.
    }
    iFrame "vs". iFrame "outlives".
    
    destruct pures as [Hdom [Hwf Hoc]].
    
    iSplitL. { iPureIntro. 
      split. { rewrite dom_insert_L. rewrite dom_insert_L. rewrite Hdom. trivial. }
      split. { apply map_wf_insert; trivial. { done. } { apply swell_set_default_dead. } set_solver. }
      trivial.
    }
    { iApply big_sepM_insert_1. iFrame "slice".
      rewrite big_sepM_forall. iIntros (x R) "%Dm".
      iApply "slices". iPureIntro. trivial. }
  Qed.
  
    
  Lemma llfb_fb_vs_for_reborrow1 alive dead al al' sn sn2 mbs mprops P k dd :
    (sn ≠ sn2) →
    alive ## dead →
    ¬(dd ## dead) →
    (al' ⊆ alive ∪ dead) →
    mbs !! sn = Some (Borrow al dd) →
    mbs !! sn2 = None →
  ▷ full_borrows_invalidated_via alive dead k
                (<[sn2:=Borrow (al ∪ al') dd]> (<[sn:=Borrow al al']> (delete sn mbs)))
                (<[sn2:=P]> (<[sn:=P]> (delete sn mprops)))
      ∗ ▷ (mprops !! sn ≡ Some P)
  ⊢ ▷ full_borrows_invalidated_via alive dead k mbs mprops
      ∗ (⌜ al ⊆ alive ∧ k ∉ al ∧ al' ⊆ alive ∧ k ∈ al' ⌝ -∗ ▷ P).
  Proof using invGS_gen0 Σ γ γd γo.
    iIntros (Hne Hdisj Halive Hdead Hmbssn Hmbssn2) "[inval #Heq]".
    destruct (decide (k ∈ al ∧ al ⊆ alive)) as [Hin|Hout].
    
    - destruct (decide (al' ⊆ alive)) as [Hsub|Hnotsub].
      + 
      iDestruct (full_borrows_invalidated_via_insert with "inval") as "[P inval]".
      { rewrite lookup_insert_ne; trivial. rewrite lookup_delete_ne; trivial. }
      { unfold invalidated. rewrite bool_decide_eq_true. set_solver. }
      iDestruct (full_borrows_invalidated_via_insert_false with "inval") as "inval".
      { apply lookup_delete. }
      iDestruct (full_borrows_invalidated_via_delete with "[P inval]") as "inval".
        { apply Hmbssn. } { iFrame "Heq". iFrame "inval". iFrame "P". }
      iFrame. iIntros "%t". exfalso. set_solver.
      +
      iDestruct (full_borrows_invalidated_via_insert_false with "inval") as "inval".
      { rewrite lookup_insert_ne; trivial. rewrite lookup_delete_ne; trivial. }
      iDestruct (full_borrows_invalidated_via_insert with "inval") as "[P inval]".
      { apply lookup_delete. }
      { unfold invalidated. rewrite bool_decide_eq_true. set_solver. }
      iDestruct (full_borrows_invalidated_via_delete with "[P inval]") as "inval".
        { apply Hmbssn. } { iFrame "Heq". iFrame "inval". iFrame "P". }
      iFrame. iIntros "%t". exfalso. set_solver.
      
    - destruct (decide (al ⊆ alive ∧ k ∉ al ∧ al' ⊆ alive ∧ k ∈ al')) as [Hx|Hy].
      +
      iDestruct (full_borrows_invalidated_via_insert with "inval") as "[P inval]".
      { rewrite lookup_insert_ne; trivial. rewrite lookup_delete_ne; trivial. }
      { unfold invalidated. rewrite bool_decide_eq_true. set_solver. }
      iDestruct (full_borrows_invalidated_via_insert_false with "inval") as "inval".
      { apply lookup_delete. }
      iDestruct (full_borrows_invalidated_via_delete_false with "inval") as "inval".
      { apply Hmbssn. } { unfold invalidated. rewrite bool_decide_eq_false. set_solver. }
      iFrame. iIntros "%Ht". done.
      +
      iDestruct (full_borrows_invalidated_via_insert_false with "inval") as "inval".
      { rewrite lookup_insert_ne; trivial. rewrite lookup_delete_ne; trivial. }
      iDestruct (full_borrows_invalidated_via_insert_false with "inval") as "inval".
      { apply lookup_delete. }
      iDestruct (full_borrows_invalidated_via_delete_false with "inval") as "inval".
      { apply Hmbssn. } { unfold invalidated. rewrite bool_decide_eq_false. set_solver. }
      iFrame. iIntros "%Ht". exfalso. set_solver.
  Qed.
  
  Lemma llfb_fb_vs_for_reborrow2 alive dead al al' sn sn2 mbs mprops P k dd :
    (sn ≠ sn2) →
    alive ## dead →
    ¬(dd ## dead) →
    (al' ⊆ alive ∪ dead) →
    mbs !! sn = Some (Borrow al dd) →
    mbs !! sn2 = None →
    ▷ full_borrows_revalidated_via alive dead k mbs mprops
      ∗ ▷ (mprops !! sn ≡ Some P)
      ∗ (⌜ al ⊆ alive ∧ k ∉ al ∧ al' ⊆ alive ∧ k ∈ al' ⌝ -∗ ▷ P)
    ⊢
    ▷ full_borrows_revalidated_via alive dead k
        (<[sn2:=Borrow (al ∪ al') dd]> (<[sn:=Borrow al al']> (delete sn mbs)))
        (<[sn2:=P]> (<[sn:=P]> (delete sn mprops))).
  Proof using invGS_gen0 Σ γ γd γo.
    iIntros (Hdisj Halive Hdead Hwfal' Hmbssn Hmbssn2) "[reval [#Heq P]]".
    
    iApply full_borrows_revalidated_via_insert_false.
      { rewrite lookup_insert_ne; trivial. rewrite lookup_delete_ne; trivial. }
      { unfold revalidated. rewrite bool_decide_eq_false. set_solver. }
    
    destruct (decide (al ⊆ alive ∧ k ∉ al ∧ al' ⊆ alive ∧ k ∈ al')) as [Ha|Hb].
    - iDestruct ("P" with "[%]") as "P"; trivial.
    
      iApply full_borrows_revalidated_via_insert.
        { rewrite lookup_delete; trivial. } iFrame.
        
      iApply full_borrows_revalidated_via_delete_false.
        { apply Hmbssn. } iFrame.
    -
      iApply full_borrows_revalidated_via_insert_false.
        { rewrite lookup_delete; trivial. } 
        { unfold revalidated. rewrite bool_decide_eq_false. set_solver. }
        
      iApply full_borrows_revalidated_via_delete_false.
        { apply Hmbssn. } iFrame.
   Qed.
          
  Lemma llfb_fb_vs_for_reborrow alive dead outlives mbs mprops sn sn2 al al' dd P :
    (sn ≠ sn2) →
    alive ## dead →
    ¬(dd ## dead) →
    (al' ⊆ alive ∪ dead) →
    mbs !! sn = Some (Borrow al dd) →
    mbs !! sn2 = None →
    future_vs γ alive dead outlives mbs mprops
      ∗ ▷ (mprops !! sn ≡ Some P)
    ⊢ 
    future_vs γ alive dead outlives
      (<[sn2:=Borrow (al ∪ al') dd]> (<[sn:=Borrow al al']> mbs))
      (<[sn2:=P]> (<[sn:=P]> (delete sn mprops))).
  Proof using invGS_gen0 Σ γ γd γo.
    replace (<[sn:=Borrow al al']> mbs) with (<[sn:=Borrow al al']> (delete sn mbs)).
      2: { apply map_eq. { intros i. destruct (decide (sn = i)).
          - subst sn. rewrite lookup_insert. rewrite lookup_insert. trivial.
          - rewrite lookup_insert_ne; trivial. rewrite lookup_insert_ne; trivial.
              rewrite lookup_delete_ne; trivial. } }
  
    intros Hne.
    generalize dead. clear dead.
    induction alive as [my_alive IH] using set_ind_strong. 
    iIntros (my_dead Hdisj Hdead Hwfal Hmbssn Hmbssn2) "[Hvs #Heq]".
    
    rewrite future_vs_def.
    rewrite (future_vs_def γ my_alive).
    iIntros (k) "%Hin_my_alive Dead". iSplit.
      { iIntros "NotSwell". iDestruct ("Hvs" $! k Hin_my_alive with "Dead") as "[Hvs _]".
        iApply IH. { intuition. } { set_solver. } { set_solver. }
        { intro x. destruct (decide (x = k)); set_solver. }
        { trivial. } { trivial. }
        iFrame "#". iApply "Hvs". iFrame.
      }
    iIntros "inval".

    iDestruct ("Hvs" $! k Hin_my_alive with "Dead") as "Hvs".
    destruct Hin_my_alive as [Hin_my_alive Hoc].
    
    iDestruct (llfb_fb_vs_for_reborrow1 my_alive my_dead al al' sn sn2 mbs mprops P k dd with "[inval]") as "[inval P]"; trivial. { iFrame. iFrame "Heq". }
     
    iMod ("Hvs" with "[inval]") as "[reval vs]". { iFrame. }

    iModIntro.
    
    iSplitL "P reval". { iApply llfb_fb_vs_for_reborrow2; trivial. iFrame. iFrame "Heq". }
    
    iApply IH; trivial. { set_solver. } { set_solver. }
      { intro x. destruct (decide (x = k)); set_solver. }
    iFrame. iFrame "#".
  Qed.
  
  Lemma lemma_set5 (al dd' alive dead : gset lt_atom) :
    al ∪ dd' ⊆ alive ∪ dead →
    al ## dead →
    al ⊆ alive. Proof. set_solver. Qed.
    
  Lemma lemma_set6 (al' dd' alive dead : gset lt_atom) :
    al'  ⊆ alive ∪ dead →
    al' ## dead →
    al' ⊆ alive. Proof. set_solver. Qed.
            
  Lemma inner_reborrow alive dead blocked P sn al al' :
      swell_set al' →
      (al' ⊆ alive ∪ dead) →
      (▷ inner_inv γ γo alive dead blocked)
        ∗ OwnBorState γ sn (Borrow al {[default_dead]})
        ∗ slice Nbox sn P
        ={↑Nbox}=∗ ∃ sn1 sn2,
      (▷ inner_inv γ γo alive dead blocked)
        ∗ OwnBorState γ sn1 (Borrow (al ∪ al') {[default_dead]})
        ∗ OwnBorState γ sn2 (Borrow al al')
        ∗ slice Nbox sn1 P
        ∗ slice Nbox sn2 P.
  Proof using invGS_gen0 Σ γ γd γo.
    intros swell_al' Hal'wf.
    iIntros "[Inv [OwnBor #sliceP]]".
    destruct (decide (al ## dead)) as [Hdisj|Hndisj].
    2: {
      iDestruct "Inv" as (mbs mprops Ptotal outlives) "[>auth [>Modulo [box [vs [#pures [#slices outlives]]]]]]".
      iMod (inner_fake γ alive dead blocked (al ∪ al') {[default_dead]} P with "Modulo") as (sn1) "[Modulo [A Aslice]]". { set_solver. }
      iMod (inner_fake γ alive dead blocked al al' P with "Modulo") as (sn2) "[Modulo [B Bslice]]". { set_solver. }
      iModIntro. iExists sn1. iExists sn2. iFrame. iFrame "#".
    }
    
    destruct (decide (al' ## dead)) as [Hdisj'|Hndisj].
    2: {
      iDestruct (inv_get_dead with "Inv") as "#X". iMod "X". iDestruct "X" as "%Hdd".
      iDestruct "Inv" as (mbs mprops Ptotal outlives) "[>auth [>Modulo [box [vs [#pures [#slices outlives]]]]]]".
      iMod (inner_fake γ alive dead blocked (al ∪ al') {[default_dead]} P with "Modulo") as (sn1) "[Modulo [A Aslice]]". { set_solver. }
      
      assert (∃ x , x ∈ al' ∩ dead) as Hex_x. { apply set_choose_L; set_solver. }
      destruct Hex_x as [x Hex].
      iDestruct (get_all_modulo_dead_pers γ dead with "Modulo") as "[Modulo #Deads]".
      iMod (update_bor_state_borrow_fix_dead γ sn al {[default_dead]} al' default_dead x with "[OwnBor]") as "OwnBor2"; trivial. { set_solver. } { set_solver. } {
        iFrame. iSplitL.
          { iApply "Deads". iPureIntro. trivial. }
          { iApply "Deads". iPureIntro. set_solver. }
      }
      
      iModIntro. iExists sn1. iExists sn. iFrame. iFrame "#".
    }
   
    unfold inner_inv.
    iDestruct "Inv" as (mbs mprops Ptotal outlives) "[>auth [>Modulo [box [vs [>%pures [#slices >outlives]]]]]]".
    
    iDestruct (agree_bor_state_borrow_lts γ sn mbs dead al {[default_dead]}
        with "[Modulo auth OwnBor]") as (dd') "[%Hmbssn %Hrel_dd]". { set_solver. } { iFrame.    }
        
    destruct pures as [Hdom [Hwf Houtlives]].
    assert (alive ## dead) as Halive_disj_dead. { unfold inv.map_wf in Hwf. intuition. }
    assert (default_dead ∈ dead) as Hdddead. { unfold inv.map_wf in Hwf. intuition. }
    assert (¬ dd' ## dead) as Hdd'dead. { set_solver. }
    assert (al ⊆ alive) as Halalive. { unfold inv.map_wf in Hwf.
      destruct Hwf as [Ha [Hb [Hc [Hforall Hforall2]]]].
      have Hf := Hforall sn _ Hmbssn. destruct Hf as [Hf _].
      apply (lemma_set5 al dd' alive dead); trivial.
    }
    assert (al' ⊆ alive) as Hal'alive. { apply (lemma_set6 al' dd' alive dead); trivial. }
    assert (dd' ⊆ alive ∪ dead) as Hdd'wf. { 
      destruct Hwf as [Ha [Hb [Hc [Hforall Hforall2]]]].
      have Hf := Hforall sn _ Hmbssn.
      set_solver.
    }
    have swell_al := swell_set_of_map_wf _ _ _ _ _ _ _ _ Hwf Hmbssn.
    have swell_dd' := swell_set_of_map_wf_de _ _ _ _ _ _ _ _ Hwf Hmbssn.
    have swell_al_al' := swell_set_union _ _ swell_al swell_al'.
        
    iMod (slice_empty Nbox (↑Nbox) true (boxmap alive dead mbs) Ptotal P sn with "sliceP box")
      as "[P box]"; trivial.
    {
      unfold boxmap. rewrite lookup_fmap. rewrite Hmbssn. simpl. f_equiv.
      rewrite bool_decide_eq_true. set_solver.
    }
        
    iMod (slice_insert_full Nbox (↑Nbox) true (<[sn:=false]> (boxmap alive dead mbs)) Ptotal P with "P box")
      as (sn2) "[%Hlookup_sn2 [#slice box]]".
    { trivial. }
    
    destruct (lookup_insert_boxmap_helper3 sn false alive dead mbs sn2 Hlookup_sn2) as [Hmbssn2 Hne].
    iMod (update_bor_state_borrow_lts γ sn _ dead al {[default_dead]} (Borrow al al') with "[Modulo auth OwnBor]") as "[Modulo [auth OwnBor]]". { set_solver. } { iFrame "auth". iFrame. }
    
    iMod (alloc_bor_state γ sn2 (<[sn:=Borrow al al']> mbs) (Borrow (al ∪ al') dd') with "[auth]") as "[auth OwnBor2]"; trivial. { rewrite lookup_insert_ne; trivial. }
    iMod (update_bor_state_borrow_fix_dead_lts γ dead sn2 (al ∪ al') dd' {[default_dead]} with "[Modulo OwnBor2]") as "[Modulo OwnBor2]". { set_solver. } { set_solver. } { iFrame. }
    
    iDestruct (bi.later_mono _ _ (llfb_fb_vs_for_reborrow alive dead outlives mbs mprops sn sn2 al al' dd' P Hne Halive_disj_dead Hdd'dead Hal'wf Hmbssn Hmbssn2)
     with "[vs]") as "vs"; trivial. { iFrame. iNext.
      iDestruct (agree_slice_with_map mbs mprops sn _ P with "[]") as "EqSlice"; trivial.
      { apply Hmbssn. } iFrame "#". }
        
    iModIntro.
    iExists sn2. iExists sn.
    iFrame "OwnBor". iFrame "OwnBor2". iFrame "slice". iFrame "sliceP". iFrame "Modulo".
    iNext.
    iExists (<[sn2:=Borrow (al ∪ al') dd']> (<[sn:=Borrow al al']> mbs)).
    iExists (<[sn2:=P]> (<[sn:=P]> (delete sn mprops))).
    iExists (P ∗ Ptotal)%I.
    iExists outlives.
    iFrame "outlives". iFrame "auth".
    iSplitL "box". {
      rewrite boxmap_insert_Borrow.
      rewrite boxmap_insert_Borrow.
      rewrite bool_decide_eq_true_2.
      - rewrite bool_decide_eq_false_2.
        + iFrame "box". 
        + set_solver.
      - set_solver.
    }
    iFrame "vs".
    iSplitL. { iPureIntro. 
      split. { rewrite dom_insert_L. rewrite dom_insert_L. rewrite dom_insert_L.
      rewrite dom_insert_L. rewrite dom_delete_L. rewrite Hdom.
        apply set_eq. intros x.
        destruct (decide (x = sn2)); destruct (decide (x = sn)); set_solver.
      }
      split. {
        apply map_wf_insert; trivial. 2: { set_solver. }
        apply map_wf_insert; trivial. set_solver.
      }
      trivial.
    }
    { iApply big_sepM_insert_1. iFrame "slice".
      iApply big_sepM_insert_1. iFrame "sliceP".
      rewrite big_sepM_forall. rewrite big_sepM_forall. iIntros (x R) "%Dm".
      iApply "slices". iPureIntro. rewrite lookup_delete_Some in Dm. intuition.
    }
   Qed.
 
  
   Lemma inner_borrow_create alive dead blocked lt P :
      swell_set lt →
      (lt ⊆ alive ∪ dead) →
      (▷ inner_inv γ γo alive dead blocked)
        ∗ LtState γ alive dead
        ∗ (▷ P)
        ={↑Nbox}=∗
        ∃ sn sn2, (▷ inner_inv γ γo alive dead blocked)
            ∗ LtState γ alive dead
            ∗ OwnBorState γ sn (Borrow lt {[ default_dead ]})
            ∗ OwnBorState γ sn2 (Borrow ∅ lt)
            ∗ slice Nbox sn P
            ∗ slice Nbox sn2 P.
  Proof using invGS_gen0 Σ γ γd γo.
    intros swell_lt Hlt. iIntros "[Inv [LtState P]]".
    iMod (inner_borrow_create_empty alive dead blocked P with "[Inv P]") as (sn) "[Inv [OwnBor #slice]]". { iFrame. }
    iMod (inner_reborrow alive dead blocked P sn ∅ lt swell_lt Hlt with "[Inv OwnBor]") as (sn1 sn2) "[Ha [Hb [Hd [He Hf]]]]".
      { iFrame. iFrame "#". }
    iModIntro. iExists sn1. iExists sn2. iFrame. replace (∅ ∪ lt) with lt by set_solver.
    iFrame.
  Qed.

  Lemma outer_borrow_create alive dead blocked lt P :
      swell_set lt →
      (lt ⊆ alive ∪ dead) →
      Delayed γd None
        ∗ (▷ outer_inv γ γo γd alive dead blocked)
        ∗ LtState γ alive dead
        ∗ (▷ P)
        ={↑Nbox}=∗
      Delayed γd None ∗
      ∃ sn sn2, (▷ outer_inv γ γo γd alive dead blocked)
          ∗ LtState γ alive dead
          ∗ OwnBorState γ sn (Borrow lt {[ default_dead ]})
          ∗ OwnBorState γ sn2 (Borrow ∅ lt)
          ∗ slice Nbox sn P
          ∗ slice Nbox sn2 P.
  Proof.
    intros swell_lt Hlt. iIntros "[Delayed [Inv H]]".
    iDestruct "Inv" as (opt_k) "[>D Inv]".
    iDestruct (delayed_agree with "[D Delayed]") as "%Heq". { iFrame "D Delayed". }
    subst opt_k.
    iMod (inner_borrow_create alive dead blocked lt P swell_lt Hlt with "[H Inv]") as (sn sn2) "[Inv H]". { iFrame. }
    iModIntro. iFrame "Delayed H". iExists None. iFrame.
  Qed.
      
  Lemma outer_reborrow alive dead blocked P sn al al' :
      swell_set al' →
      (al' ⊆ alive ∪ dead) →
      Delayed γd None
       ∗ (▷ outer_inv γ γo γd alive dead blocked)
        ∗ OwnBorState γ sn (Borrow al {[default_dead]})
        ∗ slice Nbox sn P
        ={↑Nbox}=∗ ∃ sn1 sn2,
      Delayed γd None
        ∗ (▷ outer_inv γ γo γd alive dead blocked)
        ∗ OwnBorState γ sn1 (Borrow (al ∪ al') {[default_dead]})
        ∗ OwnBorState γ sn2 (Borrow al al')
        ∗ slice Nbox sn1 P
        ∗ slice Nbox sn2 P.
  Proof.
    intros swell_al' Hal.
    iIntros "[Delayed [Inv H]]".
    iDestruct "Inv" as (opt_k) "[>D Inv]".
    iDestruct (delayed_agree with "[D Delayed]") as "%Heq". { iFrame "D Delayed". }
    subst opt_k.
    iMod (inner_reborrow alive dead blocked P sn al al' swell_al' Hal with "[H Inv]") as (sn1 sn2) "[P H]"; trivial. { iFrame. }
    iModIntro. iExists sn1. iExists sn2.
    iFrame "Delayed". iFrame "H". iExists None. iFrame.
  Qed.
End FullBorrows.
